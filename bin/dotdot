#! /usr/bin/env zsh

if [[ -z ${DOTFILES} ]] ; then
	DOTFILES=$(dirname "$(dirname "$0")")
fi

if [[ ! -d ${DOTFILES} ]] ; then
	echo "Directory '${DOTFILES}' does not exist" 1>&2
	return 1
fi

integer retcode=0
cd "${DOTFILES}"
DOTFILES=$(pwd)
for filename in * ; do
	if [[ ${filename:0:4} = dot. ]] ; then
		target=${filename:3}
		target_name="${target//--//}"
		if [[ ${HOME} = */ ]] ; then
			target="${HOME}${target_name}"
		else
			target="${HOME}/${target_name}"
		fi
		target_dir=$(dirname "${target}")

		if [[ ! -d ${target_dir} ]] ; then
			echo "[DIR] ${target_dir}"
			mkdir -p "${target_dir}"
		fi

		if [[ -L ${target} ]] ; then
			existing_target=$(readlink -f "${target}")
			if [[ ${DOTFILES}/${filename} != ${existing_target} ]] ; then
				echo "[ERR] Symlink '${target_name}' points to '${existing_target}' instead of '${DOTFILES}/${filename}'" 1>&2
				retcode=2
			else
				echo "[---] ${target_name}"
			fi
		elif [[ ! -d ${DOTFILES}/${filename} && -x ${DOTFILES}/${filename} ]] ; then
			# This is a generator program, send its output to the target file
			echo "[GEN] ${target_name}"
			output=$(OSTYPE=${OSTYPE} MACHTYPE=${MACHTYPE} CPUTYPE=${CPUTYPE} \
				"${DOTFILES}/${filename}" "${target}")
			if [[ -n ${output} ]] ; then
				cat > "${target}" <<< "${output}"
			fi
		elif [[ -r ${target} ]] ; then
			echo "Warning: '${target_name}' is not a symlink" 1>&2
			retcode=1
		else
			echo "[SYM] ${target_name}"
			ln -s "${DOTFILES}/${filename}" "${target}"
		fi
	fi
done

if whence -p systemctl &> /dev/null ; then
	# For systemd we use hard link to circumvent the "too many symbolic
	# links" issue (https://bugzilla.redhat.com/show_bug.cgi?id=955379)
	mkdir -p ~/.config/systemd/user
	for item in "${DOTFILES}"/systemd/* ; do
		echo "[LNK] .config/systemd/user/$(basename "${item}")"
		ln -f "${item}" ~/.config/systemd/user
	done
fi

exit ${retcode}
