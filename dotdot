#! /usr/bin/env zsh

if [[ -z ${DOTFILES} ]] ; then
	DOTFILES=$(dirname "$0")
fi

if [[ ! -d ${DOTFILES} ]] ; then
	echo "Directory '${DOTFILES}' does not exist" 1>&2
	return 1
fi

integer retcode=0
cd "${DOTFILES}"
DOTFILES=$(pwd)
for filename in * ; do
	if [[ ${filename:0:4} = dot. ]] ; then
		target=${filename:3}
		target="${HOME}/${target//--//}"
		target_dir=$(dirname "${target}")

		if [[ ! -d ${target_dir} ]] ; then
			echo "[DIR] ${target_dir}"
			mkdir -p "${target_dir}"
		fi

		if [[ -L ${target} ]] ; then
			existing_target=$(readlink -f "${target}")
			if [[ ${DOTFILES}/${filename} != ${existing_target} ]] ; then
				echo "[ERR] Symlink '${target}' points to '${existing_target}' instead of '${DOTFILES}/${filename}'" 1>&2
				retcode=2
			else
				echo "[---] ${target}"
			fi
		elif [[ -x ${DOTFILES}/${filename} ]] ; then
			# This is a generator program, send its output to the target file
			echo "[GEN] ${target}"
			output=$(OSTYPE=${OSTYPE} MACHTYPE=${MACHTYPE} CPUTYPE=${CPUTYPE} \
				"${DOTFILES}/${filename}" "${target}")
			if [[ -n ${output} ]] ; then
				cat > "${target}" <<< "${output}"
			fi
		elif [[ -r ${target} ]] ; then
			echo "Warning: '${target}' is not a symlink" 1>&2
			retcode=1
		else
			echo "[SYM] ${target}"
			ln -s "${DOTFILES}/${filename}" "${target}"
		fi
	fi
done
exit ${retcode}
